#4 - Which divine typologies are Levantine (local)?
#IF “Motif origin” IS “Levantine” > LOCAL (results listed by field “Divine typology”)



PREFIX god: <http://godscapes.unict.it/godscapes-ontology#>
PREFIX cidoc: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos/>

Select DISTINCT (str(?siteName) AS ?Site) (str(?acroString) AS ?Acronym)(str(?areaString) AS ?Area) (str(?idNumString) AS ?IDNumber) (STRAFTER(STR(?motifOrigin), "_MO_") AS ?MotifOrigin) (STRAFTER(STR(?divT), "#") AS ?DivineTypology)
WHERE
{
  ?record a god:GodscapesRecord.  
  ?record cidoc:P70_documents ?assign.
  ?assign a god:GodscapesAttributeAssignment.
  ?assign cidoc:P140_assigned_attribute_to ?icono. 
  ?icono a god:IconographySupport.
  ?icono cidoc:P128_carries ?motif.
  ?motif a god:IconographyMotif.

  ?t cidoc:P141_assigned ?motif.
  ?t cidoc:P140_assigned_attribute_to ?motifOrigin. 
  ?motifOrigin a god:OriginType.
  FILTER(str(?motifOrigin) = "http://godscapes.unict.it/godscapes-ontology#ICO_IM_MO_levantine").

  ?motif cidoc:P65_incorporates ?divine.  
  ?divine cidoc:P67_refers_to ?divAtt.
  ?divAtt cidoc:P2_has_type ?divT.
    

  #ID
  ?record cidoc:P1_is_identified_by ?id.
  ?id a god:GodscapesIdentifier.
  ?id cidoc:P1_is_identified_by ?acro.
  ?acro a god:SiteAcronym.
  ?acro cidoc:P3_has_note ?acroString.
  
  ?id cidoc:P1_is_identified_by ?area.
  ?area a god:Area.
  ?area cidoc:P3_has_note ?areaString.

  ?id cidoc:P1_is_identified_by ?idNum.
  ?idNum a god:IdentificationNumber.
  ?idNum cidoc:P3_has_note ?idNumString.
  
  #Site 
  ?icono cidoc:P89_falls_within ?locsite.
  ?locsite cidoc:P89_falls_within ?site.
  ?site rdfs:label ?siteName. 
     
}